plugins {
  id 'base'
  id 'java'
  id 'eclipse'
  id 'org.xtext.xtend' version "4.0.0"
}

group = 'org.eclipse.xtend.lib.annotation.etai.tests'
version = rootProject.file('VERSION').text.trim()

repositories {
  jcenter()
}

dependencies {
  implementation project(':org.eclipse.xtend.lib.annotation.etai')
  implementation 'org.eclipse.xtend:org.eclipse.xtend.core:2.+'
  implementation 'org.eclipse.xtend:org.eclipse.xtend.lib:2.+'
  implementation 'org.eclipse.xtext:org.eclipse.xtext.xbase.lib:2.+'
  implementation 'org.eclipse.xtext:org.eclipse.xtext.xbase.testing:2.+'
  testImplementation 'junit:junit:4.12'
}

sourceSets {
  main.java.srcDirs = ['src','xtend-gen']
  main.xtend.outputDir = 'xtend-gen'
  test {
    java.srcDir 'xtend-gen'
  }
}

clean {
    delete 'xtend-gen'
}

compileJava {
    options.compilerArgs += ["-Xlint:deprecation","-Xlint:unchecked"]
}

eclipse {
  project {
    natures 'org.eclipse.xtext.ui.shared.xtextNature'
    buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
  }
  classpath {
    sourceSets -= [sourceSets.test]	
  }
}

eclipseJdt.doLast {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=UTF-8')
}